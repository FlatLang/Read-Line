package flat/readline

import flat/log/Logger

class {
  let static Logger log = Logger(ReadLine.class)

  public static async question(
    String question,
    responseMatcher(String) -> Bool = { true },
    String invalidResponseMessage = null
  ) -> String => null {
    native var Char[] data
    var String response

    external {
      await new Promise((resolve) => {
        #{ReadLineGlobal.readline}.question(#{question.chars.data}, (value) => {
          #{data} = value;
          #{response} = #{String(data)};
          resolve(#{response});
          #{ReadLineGlobal.readline}.close();
        });
      });
    }

    return if (responseMatcher(response)) {
      response
    } else {
      if (invalidResponseMessage) {
        log.info(invalidResponseMessage)
      }

      question(
        question,
        responseMatcher,
        invalidResponseMessage
      )
    }
  }

  public static class ReadLineGlobal {
    external type ReadLineType
    private static var ReadLineType _readline
    visible static ReadLineType readline {
      get {
        external {
          if (!#{_readline}) {
            #{_readline} = require('readline').createInterface({
              input: process.stdin,
              output: process.stdout
            });

            #{_readline}.on("close", () => {
              #{_readline} = null;
            });
          }
        }

        return _readline;
      }
    }
  }
}